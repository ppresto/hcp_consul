# HVN belongs to an AWS organization managed by HashiCorp, first define an AWS resource share
# that allows the two organizations to share resources.

resource "aws_ram_principal_association" "example" {
  resource_share_arn = module.tgw.ram_resource_share_id
  principal          = data.terraform_remote_state.hcp_consul.outputs.provider_account_id
}

resource "aws_ram_resource_association" "example" {
  resource_share_arn = module.tgw.ram_resource_share_id
  resource_arn       = module.tgw.ec2_transit_gateway_arn
}

resource "hcp_aws_transit_gateway_attachment" "example" {
  depends_on = [
    aws_ram_principal_association.example,
    aws_ram_resource_association.example,
  ]

  hvn_id                        = data.terraform_remote_state.hcp_consul.outputs.hvn_id
  transit_gateway_attachment_id = "hcp-tgw-attachment"
  transit_gateway_id            = module.tgw.ec2_transit_gateway_id
  resource_share_arn            = module.tgw.ram_resource_share_id
}

resource "hcp_hvn_route" "route" {
  hvn_link         = data.terraform_remote_state.hcp_consul.outputs.hvn_self_link
  hvn_route_id     = "hvn-to-tgw-attachment"
  destination_cidr = var.cidr
  target_link      = hcp_aws_transit_gateway_attachment.example.self_link
}